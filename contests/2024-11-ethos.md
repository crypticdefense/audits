# Ethos Network Financial Contracts

[Ethos Network](https://audits.sherlock.xyz/contests/675) is an onchain social reputation platform. This contest focuses on the financial stakes: vouching for others, participating in reputation markets. This builds on existing Ethos Network social contracts.

## Audit Findings Summary

| ID | Title | Severity |
|----|----------|----------|
| [H-1](#h-1-reputationmarketbuyvotes-incorrectly-includes-protocol-and-donation-fees-when-tallying-market-funds-causing-dos-or-stolen-funds) | ReputationMarket::buyVotes incorrectly includes protocol and donation fees when tallying market funds, causing DoS or stolen funds | High |
| [M-1](#m-1-reputationmarketsellvotes-is-missing-slippage-protection) | ReputationMarket::sellVotes is missing slippage protection | Medium |
---

## [H-1] ReputationMarket::buyVotes incorrectly includes protocol and donation fees when tallying market funds, causing DoS or stolen funds

## Summary

ReputationMarket::buyVotes allows users to buy votes for a given market. Protocol fee and donation fee is taken from the amount spent by the user.

The protocol fee is sent immediately to the protocol fee address, whereas the donation fee is collected when the donation recipient decides to call withdrawDonations.

The problem is that at the end of the buyVotes function, the amount spent by the user, including the protocol and donation fee is added to the marketFunds for the profileId of the given market.

This is a critical issue because the protocol fees and donation fees are actually not a part of the market funds. So when withdrawGraduatedMarketFunds is called by the authorized address to collect the market funds, it will attempt to take more funds than the market holds, which will either DoS due to insufficient funds, or take extra funds away, such as ETH held for donations.

## Root Cause

To determine the root cause, let's take a look at what happens when votes are bought:

ReputationMarket.sol#L442-L493
```javascript
  function buyVotes(
    uint256 profileId,
    bool isPositive,
    uint256 expectedVotes,
    uint256 slippageBasisPoints
  ) public payable whenNotPaused activeMarket(profileId) nonReentrant {
    _checkMarketExists(profileId);

    // Determine how many votes can be bought with the funds provided
    (
      uint256 votesBought,
@>    uint256 fundsPaid, //@audit fundsPaid includes donation and protocol fee
      ,
      uint256 protocolFee,
      uint256 donation,
      uint256 minVotePrice,
      uint256 maxVotePrice
    ) = _calculateBuy(markets[profileId], isPositive, msg.value);

    ...

    // Apply fees first
    applyFees(protocolFee, donation, profileId); //@audit donation and protocol fees are applied here

    ...

    // Calculate and refund remaining funds
    uint256 refund = msg.value - fundsPaid;
    if (refund > 0) _sendEth(refund);

    // tally market funds
@>  marketFunds[profileId] += fundsPaid; //@audit this incorrectly includes protocol and donation fees
    ...
  }
```
Looking at the internal call to _calculateBuy:

ReputationMarket.sol#L942-L983
```javascript
  function _calculateBuy(
    Market memory market,
    bool isPositive,
    uint256 funds
  )
    private
    view
    returns (
      uint256 votesBought,
      uint256 fundsPaid,
      uint256 newVotePrice,
      uint256 protocolFee,
      uint256 donation,
      uint256 minVotePrice,
      uint256 maxVotePrice
    )
  {
    uint256 fundsAvailable;
@>  (fundsAvailable, protocolFee, donation) = previewFees(funds, true);
    ...

    while (fundsAvailable >= votePrice) {
      fundsAvailable -= votePrice;
      fundsPaid += votePrice;
      votesBought++;

      market.votes[isPositive ? TRUST : DISTRUST] += 1;
      votePrice = _calcVotePrice(market, isPositive);
    }
@>  fundsPaid += protocolFee + donation; //@audit protocol fee and donation fee included

    maxPrice = votePrice;

    return (votesBought, fundsPaid, votePrice, protocolFee, donation, minPrice, maxPrice);
  }
```
We can see fundsPaid includes protocol fee and donation fee. Continuing with the buyFunds function call, applyFees is called:

ReputationMarket.sol#L1116
```javascript
  function applyFees(
    uint256 protocolFee,
    uint256 donation,
    uint256 marketOwnerProfileId
  ) private returns (uint256 fees) {
    donationEscrow[donationRecipient[marketOwnerProfileId]] += donation;
    if (protocolFee > 0) {
      (bool success, ) = protocolFeeAddress.call{ value: protocolFee }("");
      if (!success) revert FeeTransferFailed("Protocol fee deposit failed");
    }
    fees = protocolFee + donation;
  }
```
Fees are applied here by sending the protocol fees directly to protocolFeeAddress and updating the donations mapping for the donation recipient, which they can later collect by calling withdrawDonations().

Next, we can see that any extra ETH sent is refunded to the caller uint256 refund = msg.value - fundsPaid;. This is precisely why fundsPaid includes the donation and fee amount, to ensure that the caller is refunded any unused ETH.

However, that same amount of fundsPaid is added to the marketFunds in the next lines:

marketFunds[profileId] += fundsPaid;.

So the root cause is right here, where the entire fee including the protocol fee and donation fee is added to the market funds.

When market funds are finally collected, the amount to collect will be inflated (will not correctly reflect the actual amount in the market):

ReputationMarket.sol#L655-L678
```javascript
  /**
   * @notice Withdraws funds from a graduated market
   * @dev Only callable by the authorized graduation withdrawal address
   * @param profileId The ID of the graduated market to withdraw from
   */
  function withdrawGraduatedMarketFunds(uint256 profileId) public whenNotPaused {
    address authorizedAddress = contractAddressManager.getContractAddressForName(
      "GRADUATION_WITHDRAWAL"
    );
    if (msg.sender != authorizedAddress) {
      revert UnauthorizedWithdrawal();
    }
    _checkMarketExists(profileId);
    if (!graduatedMarkets[profileId]) {
      revert MarketNotGraduated();
    }
    if (marketFunds[profileId] == 0) {
      revert InsufficientFunds();
    }

@>  _sendEth(marketFunds[profileId]); //@audit this will attempt to send extra eth that either the contract will not have or will indeed have that should belong for someone else
    emit MarketFundsWithdrawn(profileId, msg.sender, marketFunds[profileId]);
    marketFunds[profileId] = 0;
  }
```

## Internal pre-conditions

1. Market exists
2. User buys votes

## External pre-conditions

N/A

## Attack Path

1. Market is created
2. User buys votes
3. Authorized address calls withdrawGraduatedMarketFunds when market is graduated

## Impact

Denial of Service when withdrawing market funds via withdrawGraduatedMarketFunds due to insufficient funds causing a loss of funds for the entities that are designated to collect these funds.

If the contract has sufficient funds, then the impact is loss of funds from taking extra funds from the ReputationMarket contract, which belong to other entities such as donation recipients.

## PoC

Consider the following scenario (these numbers are hypothetical and allow for a simple example):

1. ReputationMarket currently holds 1 ETH
2. User calls buyVotes with 10 ETH. 1 ETH is taken as donation fee, and 1 ETH is taken as protocol fee. fundsPaid = 10 ETH.
3. Protocol fee of 1 ETH is sent during buyVotes call, so the contract now holds total 10 ETH. (9 ETH from user + 1 ETH starting).
4. marketFunds now equals 10 ETH due to the line marketFunds[profileId] += fundsPaid.
5. Donation recipient withdraws 1 ETH that was donated to them, so the contract now holds 9 ETH.
6. withdrawGraduatedMarketFunds is called and attempts to send marketFunds[profileId] = 10 ETH to the caller, but since contract holds 9 ETH, it will revert due to insufficient funds.

Note that if the contract does indeed hold enough ETH, others will suffer, such as donation recipient who will have their ETH stolen. In the example above, if the donation recipient did not withdraw before-hand, it could have been their funds stolen.

## Mitigation

Deduct protocol and donation fees before tallying marketFunds:
```javascript
function buyVotes(
    uint256 profileId,
    bool isPositive,
    uint256 expectedVotes,
    uint256 slippageBasisPoints
  ) public payable whenNotPaused activeMarket(profileId) nonReentrant {
    _checkMarketExists(profileId);

    // Determine how many votes can be bought with the funds provided
    (
      uint256 votesBought,
      uint256 fundsPaid,
      ,
      uint256 protocolFee,
      uint256 donation,
      uint256 minVotePrice,
      uint256 maxVotePrice
    ) = _calculateBuy(markets[profileId], isPositive, msg.value);

    _checkSlippageLimit(votesBought, expectedVotes, slippageBasisPoints);

    // Apply fees first
    applyFees(protocolFee, donation, profileId);

    // Update market state
    markets[profileId].votes[isPositive ? TRUST : DISTRUST] += votesBought;
    votesOwned[msg.sender][profileId].votes[isPositive ? TRUST : DISTRUST] += votesBought;

    // Add buyer to participants if not already a participant
    if (!isParticipant[profileId][msg.sender]) {
      participants[profileId].push(msg.sender);
      isParticipant[profileId][msg.sender] = true;
    }

    // Calculate and refund remaining funds
    uint256 refund = msg.value - fundsPaid;
    if (refund > 0) _sendEth(refund);

    // tally market funds
-   marketFunds[profileId] += fundsPaid;
+   marketFunds[profileId] += (fundsPaid - protocolFee - donation);
    emit VotesBought(
      profileId,
      msg.sender,
      isPositive,
      votesBought,
      fundsPaid,
      block.timestamp,
      minVotePrice,
      maxVotePrice
    );
    _emitMarketUpdate(profileId);
  }
```

## [M-1] ReputationMarket::sellVotes is missing slippage protection

## Summary

ReputationMarket::sellVotes is missing slippage protection, as opposed to ReputationMarket::buyVotes, which correctly uses slippage protection.

Selling votes must also allow users to set slippage protection, as the vote price can change prior to function execution, causing a loss of funds for users.

Note that the private mempool of Base L2 can still create conditions where inadequate slippage can lead to loss of funds (i.e, high network congestion leading to delay in price changes)

## Root Cause

To determine the root cause, let's look at what happens when selling votes:

ReputationMarket.sol#L495-L534
```javascript
  function sellVotes(
    uint256 profileId,
    bool isPositive,
    uint256 amount
  ) public whenNotPaused activeMarket(profileId) nonReentrant {
    _checkMarketExists(profileId);

    // calculate the amount of votes to sell and the funds received
    (
      uint256 votesSold,
      uint256 fundsReceived,
      ,
      uint256 protocolFee,
      uint256 minVotePrice,
      uint256 maxVotePrice
    ) = _calculateSell(markets[profileId], profileId, isPositive, amount);

    // update the market state
    markets[profileId].votes[isPositive ? TRUST : DISTRUST] -= votesSold;
    votesOwned[msg.sender][profileId].votes[isPositive ? TRUST : DISTRUST] -= votesSold;

    // apply protocol fees
    applyFees(protocolFee, 0, profileId);

    // send the proceeds to the seller
@>  _sendEth(fundsReceived);
    // tally market funds
    marketFunds[profileId] -= fundsReceived;
    ...
  }
```
The internal _calculateSell calculates the votes sold and funds received by calling _calcVotePrice to determine the price of the votes being sold:

ReputationMarket.sol#L912-L923
```javascript
  function _calcVotePrice(Market memory market, bool isPositive) private pure returns (uint256) {
    uint256 totalVotes = market.votes[TRUST] + market.votes[DISTRUST];
    return (market.votes[isPositive ? TRUST : DISTRUST] * market.basePrice) / totalVotes;
  }
```
We can see that the vote price is determined by totalVotes and market.votes, both which can change prior to function execution.

This means that the vote price can significantly decrease prior to a user's sellVotes function call, which will cause a loss of funds for them.

We can therefore determine that the root cause is missing slippage in sellVotes.

## Internal pre-conditions

N/A

## External pre-conditions

N/A

## Attack Path

1. User sells votes via sellVotes function
2. Vote price decreases prior to execution (i.e, other transactions are executed first)
3. Users votes are sold and receives less funds than expected

## Impact

Loss of funds for users due to receiving less funds than expected from selling votes.

## PoC

Consider the following scenario (these numbers are hypothetical and allow for a simple example):

Assume market.basePrice = 1 ETH, totalVotes = 10, market.votes[DISTRUST] = 2, market.votes[TRUST] = 5.

Alice decides to sell her two market.votes[TRUST] votes by calling sellVotes, expecting to receive 5 * 1 ETH / 10 = 5e17 ETH. (we will ignore protocol fee in this example for sake of simplicity.)

Before Alice's transaction is executed, Bob's transaction is executed first, where he is selling two market.votes[TRUST] votes.

This changes the market values to the following: market.votes[TRUST] = 5 - 2 = 3, and totalvotes = 10 - 2 = 8.

Alice's transaction is executed, and her votes are sold at a price of 3 * 1 ETH / 8 = 3.750e17 ETH.

Alice loses 1.25e17 ETH in this case. This is a simple example, the price discrepancy can be far greater, causing significant loss of funds to users.

## Mitigation

Allow users to specify slippage for sellVotes, such as minAmountOut slippage, where the user must receive at least that amount for selling their votes.
